
// Humanoid Base Class ------------------------------------------------------


class StrifeHumanoid : Actor
{
	Default
	{
		MaxStepHeight 16;
		MaxDropoffHeight 32;
		CrushPainSound "misc/pcrush";
	}
	States
	{
	Burn:
		BURN A 3 Bright A_PlaySound("human/imonfire", CHAN_VOICE);
		BURN B 3 Bright A_DropFire;
		BURN C 3 Bright A_Wander;
		BURN D 3 Bright A_NoBlocking;
		BURN E 5 Bright A_DropFire;
		BURN FGH 5 Bright A_Wander;
		BURN I 5 Bright A_DropFire;
		BURN JKL 5 Bright A_Wander;
		BURN M 5 Bright A_DropFire;
		BURN N 5 Bright;
		BURN OPQPQ 5 Bright;
		BURN RSTU 7 Bright;
		BURN V -1;
		Stop;
	Disintegrate:
		DISR A 5 A_PlaySound("misc/disruptordeath", CHAN_VOICE);
		DISR BC 5;
		DISR D 5 A_NoBlocking;
		DISR EF 5;
		DISR GHIJ 4;
		MEAT D 700;
		Stop;
	}
}

// Fire Droplet -------------------------------------------------------------

class FireDroplet : Actor
{
	Default
	{
		+NOBLOCKMAP
		+NOCLIP
	}
	States
	{
	Spawn:
		FFOT ABCD 9 Bright;
		Stop;
	}
}
	

extend class Actor
{
	//============================================================================
	//
	// A_ShootGun
	//
	//============================================================================

	void A_ShootGun()
	{
		if (!target) return;
		A_PlaySound ("monsters/rifle", CHAN_WEAPON);
		A_FaceTarget ();
		double pitch = AimLineAttack (angle, MISSILERANGE);
		LineAttack (Angle + Random2[ShootGun]() * (11.25 / 256), MISSILERANGE, pitch, 3*(random[ShootGun]() % 5 + 1), 'Hitscan', "StrifePuff");
	}
	
	// Kneeling Guy -------------------------------------------------------------

	void A_SetShadow()
	{
		bShadow = true;
		A_SetRenderStyle(HR_SHADOW, STYLE_Translucent);
	}

	void A_ClearShadow()
	{
		bShadow = false;
		A_SetRenderStyle(1, STYLE_Normal);
	}

	void A_GetHurt()
	{
		bInCombat = true;
		if ((random[HurtMe]() % 5) == 0)
		{
			A_PlaySound (PainSound, CHAN_VOICE);
			health--;
		}
		if (health <= 0)
		{
			Die (target, target);
		}
	}
}